// SPDX-License-Identifier: Apache-2.0

WHITESPACE = _{ " " | "\t" | "\n" | "\r" }

identifier = @{ (ASCII_ALPHANUMERIC | "_")+ }

full_identifier = @{ (ASCII_ALPHANUMERIC | "_" | ":" | "$")+ }

negative_sign = { "-" }

non_negative_integer = { ASCII_DIGIT+ }

integer = { negative_sign? ~ non_negative_integer }

range = { integer ~ ":" ~ integer }

packed_dimension = { "[" ~ range ~ "]" }

packed_dimensions = { packed_dimension* }

unpacked_dimensions = { packed_dimension* }

dimensions = { packed_dimensions ~ ( "$" ~ unpacked_dimensions )? }

signed_modifier = { "signed" }

logic_type = { ( "logic" | "reg" | "bit" ) ~ signed_modifier? ~ dimensions }

struct_type = { "struct" ~ ("packed")? ~ "{" ~ field_list ~ "}" ~ full_identifier ~ dimensions }

union_type = { "union" ~ ("packed")? ~ "{" ~ field_list ~ "}" ~ full_identifier ~ dimensions }

enum_type = { "enum" ~ "{" ~ variant_list ~ "}" ~ full_identifier ~ dimensions }

allowed_type = { logic_type | struct_type | union_type | enum_type }

field = { allowed_type ~ identifier }

field_list = { field ~ (";" ~ field)* ~ (";")? }

verilog_decimal = { negative_sign? ~ non_negative_integer ~ "'" ~ ("s")? ~ "d" ~ non_negative_integer }

variant = { identifier ~ "=" ~ verilog_decimal }

variant_list = { variant ~ ("," ~ variant)* ~ (";")? }

top = { allowed_type ~ EOI }
